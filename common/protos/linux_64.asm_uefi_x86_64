section .rodata

invalid_idt:
    dq 0, 0

section .data

align 16
linux_gdt64:
    dq 0

    dq 0

    dw 0xffff
    dw 0x0000
    db 0x00
    db 10011011b
    db 10101111b
    db 0x00

    dw 0xffff
    dw 0x0000
    db 0x00
    db 10010011b
    db 10001111b
    db 0x00

  .end:

align 16
linux_gdt64_ptr:
    dw (linux_gdt64.end - linux_gdt64) - 1
    dq linux_gdt64

section .text

bits 64

extern flush_irqs

global linux_spinup64
linux_spinup64:
    cli
    cld

    lgdt [rel linux_gdt64_ptr]
    lidt [rel invalid_idt]

    lea rbx, [rel .fj]
    push 0x10
    push rbx
    retfq

  .fj:
    mov eax, 0x18
    mov ds, eax
    mov es, eax
    mov fs, eax
    mov gs, eax
    mov ss, eax

    push r8
    push r9
    push rcx
    push rdx
    push rsi
    push rdi
    call flush_irqs
    pop rdi
    pop rsi
    pop rdx
    pop rcx
    pop r9
    pop r8

    mov rax, rdi

    xor ebp, ebp
    xor edi, edi
    xor ebx, ebx

    jmp rax

section .note.GNU-stack noalloc noexec nowrite progbits
